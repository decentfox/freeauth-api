WITH
    module freeauth,
    q := <optional str>$q,
    name_filter := <optional json>$name,
    username_filter := <optional json>$username,
    mobile_filter := <optional json>$mobile,
    email_filter := <optional json>$email,
    is_deleted_filter := <optional json>$is_deleted,
    created_at_filter := <optional json>$created_at,
    last_login_at_filter := <optional json>$last_login_at,

    name_filter_op := <str>name_filter['op'],
    name_filter_val := <str>name_filter['val'],
    username_filter_op := <str>username_filter['op'],
    username_filter_val := <str>username_filter['val'],
    mobile_filter_op := <str>mobile_filter['op'],
    mobile_filter_val := <str>mobile_filter['val'],
    email_filter_op := <str>email_filter['op'],
    email_filter_val := <str>email_filter['val'],
    is_deleted_filter_val := <bool>is_deleted_filter['val'],
    created_at_filter_uval := <datetime>created_at_filter['uval'],
    created_at_filter_lval := <datetime>created_at_filter['lval'],
    last_login_at_filter_uval := <datetime>last_login_at_filter['uval'],
    last_login_at_filter_lval := <datetime>last_login_at_filter['lval'],

    users := (
        SELECT User
        FILTER
        (
            true IF not EXISTS q ELSE
            .name ?? '' ILIKE q OR
            .username ?? '' ILIKE q OR
            .mobile ?? '' ILIKE q OR
            .email ?? '' ILIKE q
        ) AND
        (
            true IF NOT EXISTS name_filter ELSE
            .name = name_filter_val IF name_filter_op = 'eq' ELSE
            (.name != name_filter_val) ?? true IF name_filter_op = 'neq' ELSE
            contains(.name, name_filter_val) IF name_filter_op = 'ct' ELSE
            (NOT contains(.name, name_filter_val)) ?? true
        ) AND
        (
            true IF NOT EXISTS username_filter ELSE
            .username = username_filter_val IF username_filter_op = 'eq' ELSE
            (.username != username_filter_val) ?? true IF username_filter_op = 'neq' ELSE
            contains(.username, username_filter_val) IF username_filter_op = 'ct' ELSE
            (NOT contains(.username, username_filter_val)) ?? true
        ) AND
        (
            true IF NOT EXISTS mobile_filter ELSE
            .mobile = mobile_filter_val IF mobile_filter_op = 'eq' ELSE
            (.mobile != mobile_filter_val) ?? true IF mobile_filter_op = 'neq' ELSE
            contains(.mobile, mobile_filter_val) IF mobile_filter_op = 'ct' ELSE
            (NOT contains(.mobile, mobile_filter_val)) ?? true
        ) AND
        (
            true IF NOT EXISTS email_filter ELSE
            .email = email_filter_val IF email_filter_op = 'eq' ELSE
            (.email != email_filter_val) ?? true IF email_filter_op = 'neq' ELSE
            contains(.email, email_filter_val) IF email_filter_op = 'ct' ELSE
            (NOT contains(.email, email_filter_val)) ?? true
        ) AND
        (
            true IF NOT EXISTS is_deleted_filter ELSE
            .is_deleted = is_deleted_filter_val IF <str>is_deleted_filter['op'] in {'eq', 'ct'} ELSE
            .is_deleted != is_deleted_filter_val
        ) AND
        (
            true IF NOT EXISTS created_at_filter ELSE
            .created_at <= created_at_filter_uval IF NOT EXISTS created_at_filter_lval ELSE
            .created_at >= created_at_filter_lval IF NOT EXISTS created_at_filter_uval ELSE
            .created_at >= created_at_filter_lval AND .created_at <= created_at_filter_uval
        ) AND
        (
            true IF NOT EXISTS last_login_at_filter ELSE
            (.last_login_at <= last_login_at_filter_uval) ?? true IF NOT EXISTS last_login_at_filter_lval ELSE
            .last_login_at >= last_login_at_filter_lval IF NOT EXISTS last_login_at_filter_uval ELSE
            .last_login_at >= last_login_at_filter_lval AND .last_login_at <= last_login_at_filter_uval
        )
    ),

    total := count(users)

select (
    total := total,
    per_page := <int64>$per_page,
    page := <int64>$page,
    last := math::ceil(total / <int64>$per_page),
    rows := array_agg((
        select users {
            id,
            name,
            username,
            email,
            mobile,
            is_deleted,
            created_at,
            last_login_at
        }
        offset (<int64>$page - 1) * <int64>$per_page
        limit <int64>$per_page
    ))
);
